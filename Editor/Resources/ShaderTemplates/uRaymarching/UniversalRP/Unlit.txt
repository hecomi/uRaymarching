Shader "Raymarching/<Name>"
{

@constants uRaymarching/Constants/uRaymarching_Default_Constants_UniversalRP

Properties
{
    [Header(Base)]
    [MainColor] _Color("Color", Color) = (0.5, 0.5, 0.5, 1)

    [Header(Pass)]
    [Enum(UnityEngine.Rendering.CullMode)] _Cull("Culling", Int) = 2
    [Enum(UnityEngine.Rendering.BlendMode)] _BlendSrc("Blend Src", Float) = 5 
    [Enum(UnityEngine.Rendering.BlendMode)] _BlendDst("Blend Dst", Float) = 10
    [Toggle][KeyEnum(Off, On)] _ZWrite("ZWrite", Float) = 1

    [Header(Raymarching)]
    _Loop("Loop", Range(1, 100)) = 30
    _MinDistance("Minimum Distance", Range(0.001, 0.1)) = 0.01
    _DistanceMultiplier("Distance Multiplier", Range(0.001, 2.0)) = 1.0
@if ShadowCaster : false
    _ShadowLoop("Shadow Loop", Range(1, 100)) = 10
    _ShadowMinDistance("Shadow Minimum Distance", Range(0.001, 0.1)) = 0.01
    _ShadowExtraBias("Shadow Extra Bias", Range(-1.0, 1.0)) = 0.01
@endif
    [PowerSlider(10.0)] _NormalDelta("NormalDelta", Range(0.00001, 0.1)) = 0.0001

@block Properties
// [Header(Additional Properties)]
// _Color2("Color2", Color) = (1.0, 1.0, 1.0, 1.0)
@endblock
}

SubShader
{

Tags 
{ 
    "RenderType" = "<RenderType=Opaque|Transparent|TransparentCutout|Background|Overlay>"
    "Queue" = "<RenderQueue=Geometry|AlphaTest|Transparent|Background|Overlay|Geometry+1|Geometry-1>"
    "IgnoreProjector" = "True" 
    "RenderPipeline" = "UniversalPipeline" 
    "DisableBatching" = "True"
}

LOD <LOD=100>

HLSLINCLUDE

@if WorldSpace : false
#define WORLD_SPACE 
@endif

@if ObjectScale : false
#define OBJECT_SCALE
@endif

#define OBJECT_SHAPE_<ObjectShape=CUBE|NONE>

@if CheckIfInsideObject : false
#define CHECK_IF_INSIDE_OBJECT
@endif

#define DISTANCE_FUNCTION DistanceFunction
#define POST_EFFECT PostEffect

#include "Packages/com.unity.render-pipelines.universal/Shaders/LitInput.hlsl"
#include "<RaymarchingShaderDirectory>/Primitives.hlsl"
#include "<RaymarchingShaderDirectory>/Math.hlsl"
#include "<RaymarchingShaderDirectory>/Structs.hlsl"
#include "<RaymarchingShaderDirectory>/Utils.hlsl"

@block DistanceFunction
inline float DistanceFunction(float3 pos)
{
    float t = _Time.x;
    float a = 6 * PI * t;
    float s = pow(sin(a), 2.0);
    float d1 = Sphere(pos, 0.75);
    float d2 = RoundBox(
        Repeat(pos, 0.2),
        0.1 - 0.1 * s,
        0.1 / length(pos * 2.0));
    return lerp(d1, d2, s);
}
@endblock

#define PostEffectOutput float4

@block PostEffect
inline void PostEffect(RaymarchInfo ray, inout PostEffectOutput o)
{
    float ao = 1.0 - pow(1.0 * ray.loop / ray.maxLoop, 2);
    o.rgb *= ao;
    o.a *= pow(ao, 3);
}
@endblock

ENDHLSL

Pass
{
    Name "Unlit"

    Blend [_BlendSrc] [_BlendDst]
    ZWrite [_ZWrite]
    Cull [_Cull]

    HLSLPROGRAM

    #pragma shader_feature _ALPHAPREMULTIPLY_ON
    #pragma multi_compile_fog
    #pragma multi_compile_instancing
    #pragma prefer_hlslcc gles
    #pragma exclude_renderers d3d11_9x
    #pragma target 2.0

@if RayStopsAtDepthTexture : false
    #define RAY_STOPS_AT_DEPTH_TEXTURE
@endif
@if RayStartsFromDepthTexture : false
    #define RAY_STARTS_FROM_DEPTH_TEXTURE
@endif

    #pragma vertex Vert
    #pragma fragment Frag
    #include "<RaymarchingShaderDirectory>/ForwardUnlit.hlsl"

    ENDHLSL
}

Pass
{
    Name "DepthOnly"
    Tags { "LightMode" = "DepthOnly" }

    ZWrite On
    ColorMask 0
    Cull [_Cull]

    HLSLPROGRAM

    #pragma shader_feature _ALPHATEST_ON
    #pragma multi_compile_instancing

    #pragma prefer_hlslcc gles
    #pragma exclude_renderers d3d11_9x
    #pragma target 2.0

    #pragma vertex Vert
    #pragma fragment Frag
    #include "<RaymarchingShaderDirectory>/DepthOnly.hlsl"

    ENDHLSL
}

@if ShadowCaster
Pass
{
    Name "ShadowCaster"
    Tags { "LightMode" = "ShadowCaster" }

    ZWrite On
    ZTest LEqual
    Cull [_Cull]

    HLSLPROGRAM

    #pragma shader_feature _SMOOTHNESS_TEXTURE_ALBEDO_CHANNEL_A
    #pragma multi_compile_instancing

    #pragma prefer_hlslcc gles
    #pragma exclude_renderers d3d11_9x
    #pragma target 2.0

    #pragma vertex Vert
    #pragma fragment Frag
    #include "<RaymarchingShaderDirectory>/ShadowCaster.hlsl"

    ENDHLSL
}
@endif

}

FallBack "Hidden/Universal Render Pipeline/FallbackError"
CustomEditor "uShaderTemplate.MaterialEditor"

}